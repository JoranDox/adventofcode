(Space, advent of code 2022 day 9)
Tangled is a string (todo 16)
Fancy is a fun word (whatever, placeholder)
Minusone is everything
Knock it down (-1)
Let minustwo be minusone
Knock it down (-2)
Whisper minusone
Whisper minustwo
Plusone is experiments (11 = 1)
Plustwo is observations (12 = 2)
Neutralzero is everything (10 = 0)

(init space as " " ascii 32)
Space is all encompassing (orbit?)
Whisper it
Cast it into space
Whisper it

(R 82 L 76 D 68 U 85)
(init right as "R" ascii 82)
Goright is nebulous observations
Whisper it
Burn it
Whisper it

(init goleft = left as "L" ascii 76)
Goleft is explosions between clouds
Whisper it
Burn it
Whisper it

(init down as "D" ascii 68)
Godown is (a) binary asteroid
Whisper it
Cast it into Godown
Whisper it

(init up as "U" ascii 85)
Goup is expanded atoms
Whisper it
Burn it
Whisper it

Feeling is stretching (0)

A wreath is entangled (9)
Build it up! (10)
While a wreath isn't nothing
    Rock the rope with feeling (0)
    Rock the rope with feeling (0)
    Knock a wreath down
    (endwhile)

(ignore the rope for part one)

Whisper the rope
Whisper "Let's go!"

(some empty arrays)
Rock a rope (do we use this?)
Rock a jacket (dictionary to check if we've visited this spot yet with the first tail)
Rock a hat (dictionary to check if we've visited this spot yet with the final tail)

(counters init to zero)
Uniquecounttwo is stretching
Uniquecountten is stretching

(loop over input)
Listen to your mind (todo rename)
While your mind isn't mysterious (while input) (Shout it)
    Shatter your mind with space
    Roll your mind into your heart (your heart = direction)
    Say "direction: '" + your heart + "'"
    Roll your mind into your mind (your mind = amount)
    Say "amount: '" + your mind + "'"
    While your mind ain't nothing (for _ in range your mind)
        Say  your mind
        Knock your mind down
        Roll the rope into hx
        Roll the rope into hy
        While the rope ain't nothing (for _ in the rope)
            Roll the rope into tx
            Roll the rope into ty
            If the rope with neutralzero is tangled (16, we're at head)
                If your heart is Goup
                    Build hy up
                    (endif)
                If your heart is Godown
                    Knock hy down
                    (endif)
                If your heart is Goright
                    Build hx up
                    (endif)
                If your heart is Goleft
                    Knock hx down
                    (endif)
                (endif)
            Put hx without tx into dx (Let dx be hx without tx)
            Let dy be hy without ty
            If dx is greater than plusone
                Let mx be Plusone
                (endif)
            If dx is less than minusone
                Let mx be Minusone
                (endif)
            If dx is neutralzero
                Let mx be neutralzero
                (endif)
            If dx is minusone
                If dy is minustwo
                    Let mx be dx
                    (endif)
                If dy is plustwo
                    Let mx be dx
                    (endif)
                If dy is plusone
                    Let mx be neutralzero
                    (endif)
                If dy is neutralzero
                    Let mx be neutralzero
                    (endif)
                If dy is minusone
                    Let mx be neutralzero
                    (endif)
                (endif)
            If dx is plusone
                If dy is minustwo
                    Let mx be dx
                    (endif)
                If dy is plustwo
                    Let mx be dx
                    (endif)
                If dy is plusone
                    Let mx be neutralzero
                    (endif)
                If dy is neutralzero
                    Let mx be neutralzero
                    (endif)
                If dy is minusone
                    Let mx be neutralzero
                    (endif)
                (endif)
            If dy is greater than plusone
                Let movey be Plusone
                (endif)
            If dy is less than minusone
                Let movey be Minusone
                (endif)
            If dy is neutralzero
                Let movey be neutralzero
                (endif)
            If dy is minusone
                If dx is minustwo
                    Let movey be dy
                    (endif)
                if dx is plustwo
                    Let movey be dy
                    (endif)
                If dx is plusone
                    Let movey be neutralzero
                    (endif)
                If  dx is neutralzero
                    Let movey be neutralzero
                    (endif)
                If dx is minusone
                    Let movey be neutralzero
                    (endif)
                (endif)
            If dy is plusone
                If dx is minustwo
                    Let movey be dy
                    (endif)
                If  dx is plustwo
                    Let movey be dy
                    (endif)
                If dx is plusone
                    Let movey be neutralzero
                    (endif)
                If dx is neutralzero
                    Let movey be neutralzero
                    (endif)
                If dx is minusone
                    Let movey be neutralzero
                    (endif)
                (endif)
            Let tx be with mx
            Let ty be with movey
            Let the store be tx with space with ty (let the store be tx + " " + ty)
            Put tx with space in the store
            Let the store be with ty
            If the rope with neutralzero is tangled (16, ergo 8 left in the rope, ergo the tail is the first tail after the head)
                Put a jacket at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecounttwo up
                    Let a jacket at the store be fancy (push to uniqueness dict as itself)
                    (endif)
                (endif 9)
            If the rope is nothing (nothing left in the rope, ergo the tail is )
                Put a hat at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecountten up (push to back of uniqueness array)
                    Let a hat at the store be fancy (push to uniqueness dict as itself)
                    (endif)
                (endif 0)
            Rock a rope with hx
            Rock a rope with hy
            Let hx be tx
            Let hy be ty
            (endwhile the rope ain't nothing)
        Rock a rope with tx
        Rock a rope with ty
        Put the rope into the box (swap the rope and a Rope) (temp = the)
        Put a rope into the rope (the = a)
        Let a rope be the box (end swap) (a = temp)
        (endwhile your mind ain't nothing)
    Listen to your mind (input another line or mysterious on EOF)
    (endwhile)

Shout "p1: " + uniquecounttwo
Shout "p2: " + uniquecountten
