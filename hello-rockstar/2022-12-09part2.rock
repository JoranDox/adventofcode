(Space, advent of code 2022 day 9)
Tangled is a string (todo 16)
Fancy is a fun word (whatever, placeholder)
Minusone is everything
Knock it down (-1)
Let minustwo be minusone
Knock it down (-2)
shout minusone
shout minustwo
Plusone is experiments (11 = 1)

(init space as " " ascii 32)
Space is all encompassing (orbit?)
Whisper it
Cast it into space
Whisper it

(R 82 L 76 D 68 U 85)
(init right as "R" ascii 82)
Goright is nebulous observations
Whisper it
Burn it
Whisper it

(init goleft = left as "L" ascii 76)
Goleft is explosions between clouds
Whisper it
Burn it
Whisper it

(init down as "D" ascii 68)
Godown is (a) binary asteroid
Whisper it
Cast it into Godown
Whisper it

(init up as "U" ascii 85)
Goup is expanded atoms
Whisper it
Burn it
Whisper it

Feeling is stretching (0)

A wreath is entangled (9)
Build it up! (10)
While a wreath isn't nothing
    Rock the rope with feeling (0)
    Rock the rope with feeling (0)
    Knock a wreath down
    (endwhile)

(ignore the rope for part one)

Whisper the rope
Whisper "Let's go!"

(some empty arrays)
Rock a rope
Rock a jacket
Rock a hat
uniquecounttwo is stretching
uniquecountten is stretching

(loop over input)
Listen to your mind (todo rename)
While your mind isn't mysterious (while input) (Shout it)
    Shatter your mind with space
    Let memes be "line len after split: '" + your mind + "'"
    Roll your mind into your heart (your heart = direction)
    Let memes be "direction: '" + your heart + "'"
    Roll your mind into your mind (your mind = amount)
    Let memes be "amount: '" + your mind + "'"
    While your mind ain't nothing (for _ in range your mind)
        Let memes be your mind
        Knock your mind down
        Roll the rope into hx
        Roll the rope into hy
        While the rope ain't nothing (for _ in the rope)
            Roll the rope into tx
            Roll the rope into ty
            Let memes be "the rope len is " + the rope
            If the rope with 0 is tangled (16, we're at head)
                Let memes be "changing the head"
                If your heart is Goup
                    Let memes be "up"
                    Knock hy down (inverted for my python equivalent)
                    (endif)
                If your heart is Godown
                    Let memes be "down"
                    Build hy up (inverted for my python equivalent)
                    (endif)
                If your heart is Goright
                    Let memes be "right"
                    Build hx up
                    (endif)
                If your heart is Goleft
                    Let memes be "left"
                    Knock hx down
                    (endif)
                (endif)
            Let dx be hx - tx
            Let dy be hy - ty
            Let memes be "t    " + tx + " " + ty
            Let memes be "d    " + dx + " " + dy
            If dx is greater than 1
                Let memes be "dx = 2"
                Let mx be Plusone
                (endif)
            If dx is less than -1
                Let memes be ":dx = -2"
                Let mx be Minusone
                (endif)
            If dx is 0
                Let memes be ":dx = 0"
                Let mx be 0
                (endif)
            If dx is -1 or dx is 1
                Let memes be ":|dx| = 1"
                if dy is -2 or dy is 2
                    Let memes be "  |dy| = 2"
                    Let mx be dx
                    (endif)
                if dy is 1 or dy is 0 or dy is -1
                    Let memes be "  |dy| < 2"
                    Let mx be 0
                    (endif)
                (endif)
            If dy is greater than 1
                Let memes be ":dy = 2"
                Let movey be Plusone
                (endif)
            If dy is less than -1
                Let memes be ":dy = -2"
                Let movey be Minusone
                (endif)
            If dy is 0
                Let memes be ":dy = 0"
                Let movey be 0
                (endif)
            If dy is -1 or dy is 1
                Let memes be ":|dy| = 1"
                If dx is -2 or dx is 2
                    Let memes be "  |dx| = 2"
                    Let movey be dy
                    (endif)
                If dx is 1 or dx is 0 or dx is -1
                    Let memes be "  |dx| < 2"
                    Let movey be 0
                    (endif)
                (endif)
            Let memes be "m    " + mx + " " + movey
            Let tx be with mx
            Let ty be with movey
            Let the store be tx + space + ty (let the store be tx + " " + ty)
            Put tx with space in the store
            Let the store be with ty
            If the rope with 0 is tangled (16, ergo 8 left in the rope, ergo the tail is the first tail after the head)
                Put a jacket at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecounttwo up
                    Let a jacket at the store be fancy (push to uniqueness dict as itself)
                    Let memes be "unique locs: " + uniquecounttwo
                    (endif)
                (endif 9)
            If the rope is nothing (nothing left in the rope, ergo the tail is )
                Put a hat at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecountten up (push to back of uniqueness array)
                    Let a hat at the store be fancy (push to uniqueness dict as itself)
                    Let memes be "unique locs 10: " + uniquecountten
                    (endif)
                (endif 0)
            Rock a rope with hx
            Rock a rope with hy
            Let hx be tx
            Let hy be ty
            (endwhile the rope ain't nothing)
        Rock a rope with tx
        Rock a rope with ty
        Put the rope into the box (swap the rope and a Rope) (temp = the)
        Put a rope into the rope (the = a)
        Let a rope be the box (end swap) (a = temp)
        (endwhile your mind ain't nothing)
    Listen to your mind (input another line or mysterious on EOF)
    (endwhile)

Shout "p1: " + uniquecounttwo
Shout "p2: " + uniquecountten
