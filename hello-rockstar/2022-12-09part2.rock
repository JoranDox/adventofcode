(The emperor's new clothes?)
(Advent of Code 2022: day 9)
Tangled is a string (todo 16)
Fancy is a fun word (whatever, placeholder)
Minusone is everything
Knock it down (-1)
Let minustwo be minusone
Knock it down (-2)
Whisper minusone
Whisper minustwo
Plusone is experiments (11 = 1)
Plustwo is observations (12 = 2)
Neutralzero is everything (10 = 0)

(init space as " " ascii 32)
Space is all encompassing (orbit?)
Whisper it
Cast it into space
Whisper it

(R 82 L 76 D 68 U 85)
(init right as "R" ascii 82)
Goright is nebulous observations
Whisper it
Burn it
Whisper it

(init goleft = left as "L" ascii 76)
Goleft is explosions between clouds
Whisper it
Burn it
Whisper it

(init down as "D" ascii 68)
Godown is (a) binary asteroid
Whisper it
Cast it into Godown
Whisper it

(init up as "U" ascii 85)
Goup is expanded atoms
Whisper it
Burn it
Whisper it

Feeling is stretching (0)
Jackets are everything
Hats are everything
Opening is important (9)
Build it up
The shop is everything (0)
While the shop isn't opening (??)
    Rock the rack with jackets
    Rock the rack with hats (0) (todo: new clothes?)
    Build the shop up
    Whisper the shop
    (endwhile)

(ignore the rack for part one)
Whisper the rack
Whisper "Let's go!"

(some empty arrays)
Rock a rack (array to push the parts of the rope for the next iteration)
Rock a jacket (dictionary to check if we've visited this spot yet with the first tail)
Rock a hat (dictionary to check if we've visited this spot yet with the final tail)

(counters init to zero)
Uniquecounttwo is stretching (?)
Uniquecountten is stretching (?)

(loop over input)
Listen to your mind (todo rename)
While your mind isn't mysterious (while input) (Shout it)
    Shatter your mind with space
    Roll your mind into your heart (your heart = direction)
    Say "direction: '" + your heart + "'"
    Roll your mind into your mind (your mind = amount)
    Say "amount: '" + your mind + "'"
    While your mind ain't nothing (for _ in range your mind)
        Say  your mind
        Knock your mind down
        Roll the rack into hx
        Roll the rack into hy
        While the rack ain't nothing (for _ in the rack)
            Roll the rack into tx (do something with "it" as if it's the rack)
            Roll the rack into ty
            If the rack with neutralzero is tangled (16, we're at head)
                If your heart is Goup
                    Build hy up
                    (endif)
                If your heart is Godown
                    Knock hy down
                    (endif)
                If your heart is Goright
                    Build hx up
                    (endif)
                If your heart is Goleft
                    Knock hx down
                    (endif)
                (endif)
            Put hx without tx into dx (Let dx be hx without tx)
            Let dy be hy without ty
            If dx is greater than plusone
                Let mx be Plusone
                (endif)
            If dx is less than minusone
                Let mx be Minusone
                (endif)
            If dx is neutralzero
                Let mx be neutralzero
                (endif)
            If dx is minusone
                If dy is minustwo
                    Let mx be dx
                    (endif)
                If dy is plustwo
                    Let mx be dx
                    (endif)
                If dy is plusone
                    Let mx be neutralzero
                    (endif)
                If dy is neutralzero
                    Let mx be neutralzero
                    (endif)
                If dy is minusone
                    Let mx be neutralzero
                    (endif)
                (endif)
            If dx is plusone
                If dy is minustwo
                    Let mx be dx
                    (endif)
                If dy is plustwo
                    Let mx be dx
                    (endif)
                If dy is plusone
                    Let mx be neutralzero
                    (endif)
                If dy is neutralzero
                    Let mx be neutralzero
                    (endif)
                If dy is minusone
                    Let mx be neutralzero
                    (endif)
                (endif)
            If dy is greater than plusone
                Let movey be Plusone
                (endif)
            If dy is less than minusone
                Let movey be Minusone
                (endif)
            If dy is neutralzero
                Let movey be neutralzero
                (endif)
            If dy is minusone
                If dx is minustwo
                    Let movey be dy
                    (endif)
                if dx is plustwo
                    Let movey be dy
                    (endif)
                If dx is plusone
                    Let movey be neutralzero
                    (endif)
                If  dx is neutralzero
                    Let movey be neutralzero
                    (endif)
                If dx is minusone
                    Let movey be neutralzero
                    (endif)
                (endif)
            If dy is plusone
                If dx is minustwo
                    Let movey be dy
                    (endif)
                If  dx is plustwo
                    Let movey be dy
                    (endif)
                If dx is plusone
                    Let movey be neutralzero
                    (endif)
                If dx is neutralzero
                    Let movey be neutralzero
                    (endif)
                If dx is minusone
                    Let movey be neutralzero
                    (endif)
                (endif)
            Let tx be with mx
            Let ty be with movey
            Let the store be tx with space with ty (let the store be tx + " " + ty)
            Put tx with space in the store
            Let the store be with ty
            If the rack with neutralzero is tangled (16, ergo 8 left in the rack, ergo the tail is the first tail after the head)
                Put a jacket at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecounttwo up
                    Let a jacket at the store be fancy (push to uniqueness dict as itself)
                    (endif)
                (endif 9)
            If the rack is nothing (nothing left in the rack, ergo the tail is )
                Put a hat at the store in the bag
                If the bag is empty (or if the bag is not fancy?)
                    build uniquecountten up (push to back of uniqueness array)
                    Let a hat at the store be fancy (push to uniqueness dict as itself)
                    (endif)
                (endif 0)
            Rock a rack with hx
            Rock a rack with hy
            Let hx be tx
            Let hy be ty
            (endwhile the rack ain't nothing)
        Rock a rack with tx
        Rock a rack with ty
        Put the rack into the box (swap the rack and a rack) (temp = the)
        Put a rack into the rack (the = a)
        Let a rack be the box (end swap) (a = temp)
        (endwhile your mind ain't nothing)
    Listen to your mind (input another line or mysterious on EOF)
    (endwhile)

Shout "p1: " + uniquecounttwo
Shout "p2: " + uniquecountten
